buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.ajoberstar:grgit:2.1.0"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.0.9"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "org.ajoberstar.grgit"

import groovy.json.JsonSlurper
def packageSlurper = new JsonSlurper()
def config = packageSlurper.parse file('package.json')

if (System.getenv("BUILD_NUMBER") != null) {
    config.version += ".${System.getenv("BUILD_NUMBER")}"
}
if (System.getenv("BUILD_VARIANT") != null) {
    config.version += ".${System.getenv("BUILD_VARIANT")}"
}

version = config.version
group = "com.indemnity83.irontanks"
archivesBaseName = "irontanks"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    mappings = config.mappings_version
    runDir = "run"

    // replacing stuff in the source
    replace "\${version}", project.version
    replace "\${mcversion}", version
    // Replace @Mod.acceptedMinecraftVersions specially as it has to be a valid version in a dev environment :/
    replace "(gradle_replace_mcversion,)", "[" + version + "]"
    replace "(gradle_replace_forgeversion,)", "[" + config.forge_version + ",)"

    // Git versioning stuffs
    if (grgit != null) {
        def repo = grgit.open()
        replace "\${git_commit_hash}", repo.head().id
        replace "\${git_commit_msg}", repo.head().fullMessage.replace("\"", "\\\"").split("\n")[0]
        replace "\${git_commit_author}", repo.head().author.name
        replace "\${git_branch}", repo.branch.current().getName()
    }
}

curseforge {
    apiKey = System.getenv("CURSE_API_KEY") ?: 'no-key'
    project {
        id = '236226'
        releaseType = 'release'
        changelog = ''
        relations {
            requiredLibrary 'buildcraft'
        }
    }
}

repositories {
    maven {
        name "BuildCraft"
        url "http://www.mod-buildcraft.com/maven"
    }
}

dependencies {
    deobfCompile "com.mod-buildcraft:buildcraft:" + config.buildcraft_version
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        // ${version}   and    ${mcversion}    are the exact strings being replaced
        expand "version":project.version, "mcversion":project.minecraft.version
    }

    // copy everything else, that's not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
