/* Build info:
 - Using a release version (e.g. 8.0.4 rather than 8.0.4-SNAPSHOT or 8.0.4-0A1B2C3D4E5) requires setting the "release" system property to true
    - Add "-Drelease=true" to disallow adding the git commit hash to the end, or SNAPSHOT if we cannot find our git directory.
*/

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.ajoberstar:grgit:2.1.0"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.0.9"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "org.ajoberstar.grgit"

ext.configFile = file "build.properties"
configFile.withReader {
    // Load config. It shall from now be referenced as simply config or project.config
    def prop =  new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

def addVersionDetails = !Boolean.getBoolean("release")
if (System.getenv("BUILD_NUMBER") != null && addVersionDetails) {
    config.mod_version += "+${System.getenv("BUILD_NUMBER")}"
}
if (System.getenv("BUILD_VARIANT") != null && addVersionDetails) {
    config.mod_version += "+${System.getenv("BUILD_VARIANT")}"
}

version = config.mod_version
group = "com.indemnity83.irontanks"
archivesBaseName = "irontanks"

sourceCompatibility = targetCompatibility = config.java_compatability
compileJava {
    sourceCompatibility = targetCompatibility = config.java_compatability
}

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    mappings = config.mappings
    runDir = "run"

    // replacing stuff in the source
    replace "\${version}", project.version
    replace "\${mcversion}", version
    // Replace @Mod.acceptedMinecraftVersions specially as it has to be a valid version in a dev environment :/
    replace "(gradle_replace_mcversion,)", "[" + version + "]"
    replace "(gradle_replace_forgeversion,)", "[" + config.forge_version + ",)"

    // Git versioning stuffs
    if (grgit != null) {
        def repo = grgit.open(dir: "$projectDir")
        replace "\${git_commit_hash}", repo.head().id
        // This is very ugly: we need 7(!) \ to produce a single \ in the final one.
        replace "\${git_commit_msg}", repo.head().fullMessage.replace("\"", "\\\\\\\"").split("\n")[0]
        replace "\${git_commit_author}", repo.head().author.name
        replace "\${git_branch}", repo.branch.current().getName()
    if (addVersionDetails) {
            project.version += "+" + repo.head().id.toString().substring(0, 7)
        }
    } else {
        if (addVersionDetails) {
            project.version += "+SNAPSHOT"
        }
    }
}

curseforge {
    apiKey = System.getenv("CURSE_API_KEY") ?: 'no-key'
    project {
        id = '236226'
        releaseType = 'release'
        changelog = ''
        relations {
            requiredLibrary 'buildcraft'
        }
    }
}

repositories {
    maven {
        name "BuildCraft"
        url "http://www.mod-buildcraft.com/maven"
    }
}

dependencies {
    deobfCompile "com.mod-buildcraft:buildcraft:" + config.buildcraft_version
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        // ${version}   and    ${mcversion}    are the exact strings being replaced
        expand "version":project.version, "mcversion":project.minecraft.version
    }

    // copy everything else, that's not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
