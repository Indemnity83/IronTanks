buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.0.7"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'

def versionInfo = getGitVersion()
def gitLog = getGitLog()

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.11.2-13.20.0.2299"
    mappings = "snapshot_20170519"
    runDir = "run"
}

curseforge {
    apiKey = System.getenv("CURSE_API_KEY") ?: 'no-key'
    project {
        id = '236226'
        releaseType = 'beta'
        changelog = gitLog['message']
        relations {
            requiredLibrary 'buildcraft'
        }
    }
}

version = "${project.minecraft.version}-${versionInfo['IronTanks.version']}"
group = "com.indemnity83.irontanks"
archivesBaseName = "irontanks"

repositories {
    maven {
        name "BuildCraft"
        url "http://www.mod-buildcraft.com/maven"
    }
}

dependencies {
    compile "com.mod-buildcraft:buildcraft:7.99.6"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    // generate version.properties file from the git version data earlier
    doLast {
        def propsFile = new File(destinationDir, 'version.properties')
        def properties = new Properties()
        properties.putAll(versionInfo)
        properties['IronTanks.build.mcversion'] = project.minecraft.version
        properties.store(propsFile.newWriter(), null)
    }
}

// This is a special task for pulling the version information from git and the environment (for BUILD_NUMBER)
def getGitVersion()
{
	def out = [:]

	// call git command.
	def outStream = new ByteArrayOutputStream()
	def result = exec {
		executable = 'git'
		args = [ 'describe', '--long']
		standardOutput = outStream
	}

	def fullVersion = outStream.toString().trim()
	def matcher = fullVersion =~ /(\d+).(\d+)-(\d+)-(.*)/

	def maj = matcher[0][1]
	def min = matcher[0][2]
	def rev = matcher[0][3]
	def bn = System.getenv("PROMOTED_NUMBER") ?: System.getenv("BUILD_NUMBER") ?: matcher[0][4].toString()

	out['IronTanks.build.major.number'] = maj.toString()
	out['IronTanks.build.minor.number'] = min.toString()
	out['IronTanks.build.revision.number'] = rev.toString()
	out['IronTanks.build.githash'] = matcher[0][4].toString()
	out['IronTanks.build.number' ] = bn.toString()
	out['IronTanks.version' ] = "${maj}.${min}.${rev}.${bn}".toString()

	return out
}

def getGitLog()
{
	def out = [:]

	// call git command.
	def outStream = new ByteArrayOutputStream()
	def result = exec {
		executable = 'git'
		args = [ 'log', '--format=short', '-n 1', 'HEAD']
		standardOutput = outStream
	}

	out['message'] = outStream.toString().trim()

    return out
}
